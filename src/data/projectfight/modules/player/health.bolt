function ~/init:
    scoreboard objectives add projectfight.player.max_health dummy
    scoreboard objectives add projectfight.player.health dummy

    scoreboard objectives add projectfight.player.damage_to_deal dummy
    scoreboard objectives add projectfight.player.weakness_modifier dummy
    scoreboard objectives add projectfight.player.strength_modifier dummy

    scoreboard objectives add projectfight.player.death_timer dummy
    scoreboard objectives add projectfight.gui.hud.kill_message.remove_time dummy

function ~/tick:
    execute if score @s projectfight.gui.hud.kill_message.remove_time matches 1..:
        scoreboard players remove @s projectfight.gui.hud.kill_message.remove_time 1
        execute if score @s projectfight.gui.hud.kill_message.remove_time matches 0:
            execute store result storage projectfight:gui hud.update_hud.id int 1 run scoreboard players get @s projectfight.player.id
            function ~/../update_hud/death/remove with storage projectfight:gui hud.update_hud
    execute as @s[tag=projectfight.player.dead]:
        scoreboard players remove @s projectfight.player.death_timer 1
        execute if score @s projectfight.player.death_timer matches 0 run function projectfight:player/health/respawn

function ~/apply_damage:
    # Calculates damage_to_deal and applies it to every player with the tag `projectfight.player.temp.victim`
    execute as @a[tag=projectfight.player.temp.victim,tag=!projectfight.player.dead,gamemode=!creative]:
        # damage to deal * victim weakness
        scoreboard players operation @s projectfight.player.damage_to_deal *= @s projectfight.player.weakness_modifier
        # damage to deal * attacker strength
        scoreboard players operation @s projectfight.player.damage_to_deal *= @p[tag=projectfight.player.temp.attacker] projectfight.player.strength_modifier

        scoreboard players operation @s projectfight.player.health -= @s projectfight.player.damage_to_deal
        
        execute if score @s projectfight.player.damage_to_deal matches ..0 if score @s projectfight.player.health > @s projectfight.player.max_health:
            scoreboard players operation @s projectfight.player.health = @s projectfight.player.max_health
            
        execute if score @s projectfight.player.health matches ..0:
            scoreboard players set @s projectfight.player.health 0
            function projectfight:player/health/kill

        scoreboard players set @s projectfight.player.damage_to_deal 0
        
        function ~/../update_hud/health

    tag @a[tag=projectfight.player.temp.victim] remove projectfight.player.temp.victim
    tag @a[tag=projectfight.player.temp.attacker] remove projectfight.player.temp.attacker

function ~/kill:
    tag @s add projectfight.player.dead

    function ~/../show_death_message with storage projectfight:health temp.damage
    
    function projectfight:player/movement/knockback/disable
    scoreboard players set @s projectfight.player.death_timer 86
    data modify storage scrfx:in id set value "projectfight:death"
    function scrfx:play
    gamemode spectator @s

function ~/show_death_message:
    execute if score @s projectfight.player.id = @p[tag=projectfight.player.temp.attacker] projectfight.player.id run return:
        tellraw @s {translate:"projectfight.death.generic.self",color:"red",italic: true}
    $tellraw @s {translate:"$(translation_key).victim",with:[{"selector":"@p[tag=projectfight.player.temp.attacker]",color:"red"}],color:"gray"}

    $summon text_display ~ ~ ~ {Tags:["projectfight.gui.kill_message.resolver"],text:{translate:"$(translation_key).attacker",with:[{"selector":"@a[tag=projectfight.player.temp.victim,tag=projectfight.player.dead]",color:"gold"}],color:"white"}}
    data modify storage projectfight:health temp.damage.attacker.message set from entity @n[type=text_display,tag=projectfight.gui.kill_message.resolver] text
    kill @n[tag=projectfight.gui.kill_message.resolver]
    execute store result storage projectfight:gui hud.update_hud.id int 1 run scoreboard players get @p[tag=projectfight.player.temp.attacker] projectfight.player.id
    execute as @p[tag=projectfight.player.temp.attacker] run function ~/../update_hud/death/set_values with storage projectfight:gui hud.update_hud

function ~/respawn:
    tag @s remove projectfight.player.dead
    scoreboard players operation @s projectfight.player.health = @s projectfight.player.max_health
    gamemode adventure @s
    tp @s 0 63 0

    function ~/../update_hud/health

function ~/update_hud/death/set_values:
    scoreboard players set @s projectfight.gui.hud.kill_message.remove_time 100
    $data modify storage projectfight:players "$(id)".hud.kill_message.component set from storage projectfight:health temp.damage.attacker.message

function ~/update_hud/death/remove:
    $data remove storage projectfight:players "$(id)".hud.kill_message.component

function ~/update_hud/health:
    execute store result storage projectfight:gui hud.update_hud.id int 1 run scoreboard players get @s projectfight.player.id
    function ~/set_values with storage projectfight:gui hud.update_hud

function ~/update_hud/health/set_values:
    $execute store result storage projectfight:players "$(id)".hud.health.value int 1 run scoreboard players get @s projectfight.player.health
    $data modify storage projectfight:players "$(id)".hud.health.text set string storage projectfight:players "$(id)".hud.health.value

    $execute if score @s projectfight.player.health matches 67..100 run data modify storage projectfight:players "$(id)".hud.health.color set value "green"
    $execute if score @s projectfight.player.health matches 34..66 run data modify storage projectfight:players "$(id)".hud.health.color set value "yellow"
    $execute if score @s projectfight.player.health matches 0..33 run data modify storage projectfight:players "$(id)".hud.health.color set value "red"