# HEALTH
function ~/init:
    scoreboard objectives add projectfight.player.max_health dummy
    scoreboard objectives add projectfight.player.health dummy

    scoreboard objectives add projectfight.player.damage_to_deal dummy
    scoreboard objectives add projectfight.player.weakness_modifier dummy
    scoreboard objectives add projectfight.player.strength_modifier dummy

    scoreboard objectives add projectfight.player.death_timer dummy

function ~/tick:
    execute as @s[tag=projectfight.player.dead]:
        scoreboard players remove @s projectfight.player.death_timer 1
        execute if score @s projectfight.player.death_timer matches 0 run function projectfight:player/health/respawn

function ~/apply_damage:
    # Calculates damage_to_deal and applies it to every player with the tag `projectfight.player.temp.victim`
    execute as @a[tag=projectfight.player.temp.victim,tag=!projectfight.player.dead]:
        # damage to deal * victim weakness
        scoreboard players operation @s projectfight.player.damage_to_deal *= @s projectfight.player.weakness_modifier
        # damage to deal * attacker strength
        scoreboard players operation @s projectfight.player.damage_to_deal *= @p[tag=projectfight.player.temp.attacker] projectfight.player.strength_modifier

        scoreboard players operation @s projectfight.player.health -= @s projectfight.player.damage_to_deal

        tellraw @a {score:{name:"PilkeySEK",objective:"projectfight.player.damage_to_deal"}}

        execute if score @s projectfight.player.damage_to_deal matches ..0 if score @s projectfight.player.health > @s projectfight.player.max_health:
            scoreboard players operation @s projectfight.player.health = @s projectfight.player.max_health
            
        execute if score @s projectfight.player.health matches ..0:
            scoreboard players set @s projectfight.player.health 0
            function projectfight:player/health/kill

        scoreboard players set @s projectfight.player.damage_to_deal 0

    tag @a[tag=projectfight.player.temp.victim] remove projectfight.player.temp.victim
    tag @a[tag=projectfight.player.temp.attacker] remove projectfight.player.temp.attacker

function ~/kill:
    tag @s add projectfight.player.dead

    execute if entity @p[tag=projectfight.player.temp.attacker]: 
        tellraw @s [{"selector":"@p[tag=projectfight.player.temp.attacker]"},{"text":" killed you"}]
        tellraw @p[tag=projectfight.player.temp.attacker] [{"text":"You killed "},{"selector":"@s"}]

    execute unless entity @p[tag=projectfight.player.temp.attacker] run tellraw @s "An unknown force killed you."

    scoreboard players set @s projectfight.player.death_timer 100
    gamemode spectator @s

function ~/respawn:
    tag @s remove projectfight.player.dead
    scoreboard players operation @s projectfight.player.health = @s projectfight.player.max_health
    gamemode adventure @s
    tp @s 0 2 0