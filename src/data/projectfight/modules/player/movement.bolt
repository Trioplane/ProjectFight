from projectfight:player/main import summon_player_display

speed_levels = [
    [0, 4.3],
    [4.3, 8],
    [8, 100]
]

for index, speed_level in enumerate(speed_levels):
    predicate f"projectfight:player/movement/speed_level_{index}" {
        "condition": "minecraft:entity_properties",
        "entity": "this",
        "predicate": {
            "movement": {
                "horizontal_speed": {
                    "min": speed_level[0],
                    "max": speed_level[1]
                }
            }
        }
    }

predicate projectfight:player/movement/is_pressing_jump {
    "condition": "minecraft:entity_properties",
    "entity": "this",
    "predicate": {
        "type_specific": {
            "type": "minecraft:player",
            "input": {
                "jump": true
            }
        }
    }
}

predicate projectfight:player/movement/is_pressing_sneak {
    "condition": "minecraft:entity_properties",
    "entity": "this",
    "predicate": {
        "type_specific": {
            "type": "minecraft:player",
            "input": {
                "sneak": true
            }
        }
    }
}

predicate projectfight:player/movement/is_crawling {
    "condition": "minecraft:entity_properties",
    "entity": "this",
    "predicate": {
        "flags": {
            "is_swimming": true 
        }
    }
}

predicate projectfight:player/movement/is_on_ground {
    "condition": "minecraft:entity_properties",
    "entity": "this",
    "predicate": {
        "flags": {
            "is_on_ground": true
        }
    }
}

KNOCKBACK_PUSH_VECTOR_ENTITY = "000003a2-0000-0000-0000-000000000000"

function ~/init:
    scoreboard objectives add projectfight.player.movement.ledge_grab.cooldown dummy
    scoreboard objectives add projectfight.player.movement.slide.cooldown dummy
    scoreboard objectives add projectfight.player.movement.slide.time dummy
    scoreboard objectives add projectfight.player.movement.slide.shulker.owner dummy
    scoreboard objectives add projectfight.player.movement.air_dash.cooldown dummy
    scoreboard objectives add projectfight.player.movement.knockback.stun_time dummy

    execute unless entity @n[type=marker,tag=projectfight.player.movement.knockback.push_vector] run summon marker 0.0 0.0 0.0 {Tags:["projectfight.player.movement.knockback.push_vector"],UUID:[I;930,0,0,0]}

function ~/tick:
    execute 
        if entity @s[tag=!projectfight.player.dead, tag=!projectfight.player.knockback_stunned] 
        at @s run function projectfight:player/movement/move
    scoreboard players operation #compare_value projectfight.predicate.compare_score = @s projectfight.player.id
    execute if score @s projectfight.player.movement.ledge_grab.cooldown matches 1.. run scoreboard players remove @s projectfight.player.movement.ledge_grab.cooldown 1
    execute if score @s projectfight.player.movement.air_dash.cooldown matches 1.. run scoreboard players remove @s projectfight.player.movement.air_dash.cooldown 1
    execute if score @s projectfight.player.movement.slide.cooldown matches 1.. run scoreboard players remove @s projectfight.player.movement.slide.cooldown 1
    execute if score @s projectfight.player.movement.slide.time matches 1..:
        execute at @s as @e[type=item_display,tag=projectfight.player.movement.slide.shulker_vehicle,predicate=projectfight:util/compare_score/player.movement.slide.shulker.owner] positioned ~ ~1 ~ rotated ~ 0:
                tp @s ^ ^ ^1
        scoreboard players remove @s projectfight.player.movement.slide.time 1
    execute if score @s projectfight.player.movement.slide.time matches 0 as @e[type=item_display,tag=projectfight.player.movement.slide.shulker_vehicle,predicate=projectfight:util/compare_score/player.movement.slide.shulker.owner] at @s:
        tp @s ~ -256 ~
        execute on passengers run kill @s
        kill @s

    execute as @s[tag=projectfight.player.knockback_stunned] at @s:
        execute rotated ~ 0 run tp @n[type=item_display,tag=projectfight.player.display,predicate=projectfight:util/compare_score/player.display.owner] ~ ~ ~

        execute unless predicate projectfight:player/movement/is_on_ground run return:
            particle minecraft:poof
            particle minecraft:campfire_cosy_smoke
            execute unless score @s projectfight.player.movement.knockback.stun_time matches ..0 run return:
                scoreboard players remove @s projectfight.player.movement.knockback.stun_time 1

        execute if score @s projectfight.player.movement.knockback.stun_time matches ..0 run return:
            function projectfight:player/movement/knockback/disable
        
        scoreboard players remove @s projectfight.player.movement.knockback.stun_time 4

def player_motion_launch_xyz(x,y,z):
    scoreboard players set $x player_motion.api.launch x
    scoreboard players set $y player_motion.api.launch y
    scoreboard players set $z player_motion.api.launch z
    function player_motion:api/launch_xyz

function ~/move:
    # ACCELERATION
    execute function projectfight:player/movement/accelerate:
        execute if predicate projectfight:player/movement/speed_level_0 run return:
            attribute @s minecraft:movement_speed base set 0.1
            attribute @s minecraft:jump_strength base set 0.4
        execute if predicate projectfight:player/movement/speed_level_1 run return:
            attribute @s minecraft:movement_speed base set 0.16
            attribute @s minecraft:jump_strength base set 0.45
        execute if predicate projectfight:player/movement/speed_level_2 run return:
            attribute @s minecraft:movement_speed base set 0.23
            attribute @s minecraft:jump_strength base set 0.65

    # LEDGE GRABBING
    execute 
        if block ~ ~-0.2 ~ #air 
        positioned ~ ~-0.6 ~ anchored eyes unless block ^ ^ ^0.75 #air 
        positioned ~ ~0.5 ~ anchored eyes if block ^ ^ ^0.75 #air 
        if predicate projectfight:player/movement/is_pressing_jump
        if score @s projectfight.player.movement.ledge_grab.cooldown matches 0:
            tp @s @s
            player_motion_launch_xyz(0,6000,0)
            scoreboard players set @s projectfight.player.movement.ledge_grab.cooldown 20
            playsound minecraft:entity.breeze.jump master @s ~ ~ ~

    # SLIDING
    execute 
        if predicate projectfight:player/movement/is_pressing_sneak
        unless block ~ ~-0.1 ~ #air 
        positioned ~ ~1 ~ if entity @s[dy=0]
        positioned ~ ~-1 ~
        if score @s projectfight.player.movement.slide.cooldown matches 0
        run function projectfight:player/movement/slide:
            tag @s add projectfight.player.temp.movement.slide
            summon item_display ~ ~1 ~ {Tags:["projectfight.player.movement.slide.shulker_vehicle","projectfight.player.temp.movement.slide.shulker_vehicle"],Passengers:[{id:"minecraft:shulker",NoAI:true,Invulnerable:true,Color:12b,attributes:[{id:"minecraft:scale",base:1.75}],active_effects:[{id:"invisibility",duration:-1,show_particles:false}]}]}
            execute as @n[type=item_display,tag=projectfight.player.temp.movement.slide.shulker_vehicle]:
                scoreboard players operation @s projectfight.player.movement.slide.shulker.owner = @p[tag=projectfight.player.temp.movement.slide] projectfight.player.id

            execute positioned 0.0 0.0 0.0 rotated ~ 0 run summon marker ^ ^ ^2 {Tags:["projectfight.player.temp.movement.slide.vector"]}
            execute store result score $x player_motion.api.launch run data get entity @n[type=marker,tag=projectfight.player.temp.movement.slide.vector] Pos[0] 10000
            execute store result score $z player_motion.api.launch run data get entity @n[type=marker,tag=projectfight.player.temp.movement.slide.vector] Pos[2] 10000
            scoreboard players set $y player_motion.api.launch -500
            function player_motion:api/launch_xyz

            kill @n[type=marker,tag=projectfight.player.temp.movement.slide.vector]
            tag @s remove projectfight.player.temp.movement.slide
            tag @n[type=item_display,tag=projectfight.player.temp.movement.slide.shulker_vehicle] remove projectfight.player.temp.movement.slide.shulker_vehicle

            playsound minecraft:entity.breeze.slide master @s ~ ~ ~

            scoreboard players set @s projectfight.player.movement.slide.cooldown 20
            scoreboard players set @s projectfight.player.movement.slide.time 10

    # AIR DASH
    execute
        unless score @s projectfight.player.movement.slide.cooldown matches 1..
        if predicate projectfight:player/movement/is_pressing_sneak
        if block ~ ~0.1 ~ #air
        positioned ~ ~1 ~ if entity @s[dy=0]
        positioned ~ ~-1 ~
        if score @s projectfight.player.movement.air_dash.cooldown matches 0
        run function projectfight:player/movement/air_dash:
            execute positioned 0.0 0.0 0.0 rotated ~ 0 run summon marker ^ ^ ^2 {Tags:["projectfight.player.temp.movement.slide.vector"]}
            execute store result score $x player_motion.api.launch run data get entity @n[type=marker,tag=projectfight.player.temp.movement.slide.vector] Pos[0] 4000
            execute store result score $z player_motion.api.launch run data get entity @n[type=marker,tag=projectfight.player.temp.movement.slide.vector] Pos[2] 4000
            scoreboard players set $y player_motion.api.launch -1000
            function player_motion:api/launch_xyz

            playsound minecraft:entity.breeze.shoot master @s ~ ~ ~ 0.5
            playsound minecraft:entity.breeze.idle_air master @s ~ ~ ~ 0.5

            kill @n[type=marker,tag=projectfight.player.temp.movement.slide.vector]
            scoreboard players set @s projectfight.player.movement.air_dash.cooldown 30
            scoreboard players set @s projectfight.player.movement.slide.cooldown 10

function ~/knockback/enable:
    attribute @s minecraft:movement_speed modifier add projectfight:knockback_stun -1 add_multiplied_total
    attribute @s minecraft:movement_efficiency modifier add projectfight:knockback_stun -1 add_multiplied_total
    attribute @s minecraft:jump_strength modifier add projectfight:knockback_stun -1 add_multiplied_total
    
    effect give @s invisibility infinite 0 true
    summon_player_display("knockback_on_air")

    scoreboard players set @s projectfight.player.movement.knockback.stun_time 120
    tag @s add projectfight.player.knockback_stunned

function ~/knockback/disable:
    attribute @s minecraft:movement_speed modifier remove projectfight:knockback_stun
    attribute @s minecraft:movement_efficiency modifier remove projectfight:knockback_stun
    attribute @s minecraft:jump_strength modifier remove projectfight:knockback_stun

    effect clear @s invisibility

    execute as @n[type=item_display,tag=projectfight.player.display,predicate=projectfight:util/compare_score/player.display.owner]:
        function animated_java:player_display/remove/this

    tag @s remove projectfight.player.knockback_stunned

function ~/knockback/apply:
    execute store result score $x player_motion.api.launch run data get entity KNOCKBACK_PUSH_VECTOR_ENTITY Pos[0] 1000
    execute store result score $y player_motion.api.launch run data get entity KNOCKBACK_PUSH_VECTOR_ENTITY Pos[1] 1000
    execute store result score $z player_motion.api.launch run data get entity KNOCKBACK_PUSH_VECTOR_ENTITY Pos[2] 1000
    function player_motion:api/launch_xyz

    function ~/../enable