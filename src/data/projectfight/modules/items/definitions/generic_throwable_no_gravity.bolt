function ~/use:
    tag @s add projectfight.player.items.temp.owner

    execute anchored eyes at @s: 
        summon marker ^ ^ ^5 {Tags:["projectfight.utils.temp"]}
        summon item_display ^ ^ ^1.5 {
            Tags: ["projectfight.items.generic_throwable_no_gravity", "projectfight.items", "projectfight.items.temp",],
            item: { id: "popped_chorus_fruit" },
            teleport_duration: 2
        }

    execute as @n[type=item_display,tag=projectfight.items.temp]: 
        rotate @s facing entity @n[type=marker,tag=projectfight.utils.temp]

        scoreboard players operation @s projectfight.items.speed = .speed projectfight.items.config.generic_throwable_no_gravity
        function projectfight:items/main/give_id
        scoreboard players operation @s projectfight.items.owner = @p[tag=projectfight.player.items.temp.owner] projectfight.player.id

    playsound minecraft:entity.shulker.shoot master @a ~ ~ ~ 0.5
    playsound minecraft:entity.arrow.shoot master @a ~ ~ ~ 0.5

    kill @n[type=marker,tag=projectfight.utils.temp]
    tag @n[type=item_display,tag=projectfight.items.temp] remove projectfight.items.temp
    tag @s remove projectfight.player.items.temp.owner

function ~/movement_step:
    execute store result storage projectfight:items temp.item.speed float 0.001 run scoreboard players get @s projectfight.items.speed
    function projectfight:items/movement/move/no_gravity with storage projectfight:items temp.item

    execute at @s run function projectfight:items/definitions/generic_throwable_no_gravity/collision_check

function ~/collision_check:
    execute if function projectfight:items/collision/check/basic_extended run function projectfight:items/definitions/generic_throwable_no_gravity/on_impact

function ~/on_impact:
    tag @s add projectfight.items.temp.impact
    execute as @a if score @s projectfight.player.id = @n[type=item_display,tag=projectfight.items.temp.impact] projectfight.items.owner:
        tag @s add projectfight.player.temp.attacker

    execute as @a[distance=..5]:
        tag @s add projectfight.player.temp.victim
        scoreboard players operation @s projectfight.player.damage_to_deal = .damage projectfight.items.config.generic_throwable_no_gravity

    function projectfight:player/health/apply_damage

    particle campfire_cosy_smoke ~ ~ ~ 1 1 1 0.3 10
    particle poof ~ ~ ~ 0.3 0.3 0.3 0.2 15
    particle flash ~ ~ ~ 0 0 0 0 1

    playsound entity.wither.break_block master @a ~ ~ ~ 0.5
    playsound entity.generic.explode master @a ~ ~ ~ 0.5
    execute as @e[type=item_display,tag=projectfight.items.initial_rotation] if score @s projectfight.items.owner = @n[type=item_display,tag=projectfight.items.temp.impact] projectfight.items.id:
        kill @s

    kill @s