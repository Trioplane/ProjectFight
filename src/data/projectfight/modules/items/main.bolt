advancement projectfight:use_item {
  "criteria": {
    "use_item": {
      "trigger": "minecraft:using_item",
      "conditions": {
        "item": {
          "predicates": {
            "minecraft:custom_data": {
              "projectfight": { "is_item": true }
            }
          }
        }
      }
    }
  },
  "rewards": {
    "function": "projectfight:items/main/use_item"
  }
}

function ~/init:
    scoreboard objectives add projectfight.player.item.is_using dummy
    scoreboard objectives add projectfight.player.item.using_time dummy
    scoreboard objectives add projectfight.items.id dummy

    scoreboard objectives add projectfight.items.owner dummy

    function projectfight:items/movement/init
    function projectfight:items/registry/register

function ~/tick:
    function projectfight:items/movement/run_movement_functions

function ~/use_item:
    scoreboard players add @s projectfight.player.item.using_time 1
    execute store result storage projectfight:items temp.player.id int 1 run scoreboard players get @s projectfight.player.id

    function projectfight:items/main/use_item/run_current_item with storage projectfight:items temp.player

function ~/use_item/tick:
    execute store result score @s projectfight.player.item.is_using run advancement revoke @s only projectfight:use_item
    execute if score @s projectfight.player.item.is_using matches 0 run scoreboard players set @s projectfight.player.item.using_time 0

function ~/use_item/run_current_item:
    $execute unless data storage projectfight:players "$(id)".current_item run return run tellraw @s "[ProjectFight] error: no current item."
    $function projectfight:items/main/use_item/run_item_function with storage projectfight:players "$(id)".current_item

function ~/use_item/run_item_function:
    $execute if score .hold_duration projectfight.items.config.$(item_name) matches 1.. run return run function projectfight:items/main/use_item/try_run_hold_item_function {item_name: "$(item_name)"}
    $function projectfight:items/definitions/$(item_name)/use with storage projectfight:items temp.player
    clear @s *[custom_data~{projectfight:{is_item:true}}] 1

function ~/use_item/try_run_hold_item_function:
    $execute if score @s projectfight.player.item.using_time < .hold_duration projectfight.items.config.$(item_name) run return fail
    $function projectfight:items/definitions/$(item_name)/use with storage projectfight:items temp.player
    clear @s *[custom_data~{projectfight:{is_item:true}}] 1

function ~/give_id:
    execute store result score @s projectfight.items.id run scoreboard players add .projectfight.var.nextid projectfight.items.id 1